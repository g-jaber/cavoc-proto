module type TYPED = sig
  module Name : Names.CONT_NAMES

  type typ

  val string_of_type : typ -> string

  type negative_type

  val exception_type : typ
  val get_negative_type : typ -> negative_type option
  val string_of_negative_type : negative_type -> string

  type name_ctx = (Name.name, negative_type) Util.Pmap.pmap

  val empty_name_ctx : name_ctx
  val concat_name_ctx : name_ctx -> name_ctx -> name_ctx
  val string_of_name_ctx : name_ctx -> string
  val get_names_from_name_ctx : name_ctx -> Name.name list
end

module type STORE = sig
  type store

  val string_of_store : store -> string
  val empty_store : store

  type store_ctx

  val empty_store_ctx : store_ctx
  val string_of_store_ctx : store_ctx -> string
  val concat_store_ctx : store_ctx -> store_ctx -> store_ctx
  val infer_type_store : store -> store_ctx

  (* update_store μ μ' is equal to μ[μ'] *)
  val update_store : store -> store -> store
  val restrict : store_ctx -> store -> store

  type label

  val restrict_ctx : store_ctx -> label list -> store_ctx

  module M : Util.Monad.BRANCH
  (* *)

  val generate_store : store_ctx -> store M.m
end

module type COMP = sig
  include TYPED
  module Store : STORE

  type term

  val string_of_term : term -> string

  type value

  val string_of_value : value -> string

  type negative_val

  val filter_negative_val : value -> negative_val option
  val string_of_negative_val : negative_val -> string

  type interactive_env = (Name.name, negative_val) Util.Pmap.pmap

  val empty_ienv : interactive_env
  val concat_ienv : interactive_env -> interactive_env -> interactive_env
  val string_of_ienv : interactive_env -> string

  type opconf = term * Store.store

  (* Evaluation of programs may produces:
     - divergence
     - uncatchable error
     - raised exception that has not (yet) been caught
     - values
     - callbacks to function provided by another module
  *)

  val normalize_opconf : opconf -> opconf option
  val get_typed_term : string -> in_channel -> term * typ * name_ctx

  (* We retrive a module declaration and its signature from the two in_channel taken as input.
     We evaluate the list of computation declarations into a list of value declarations together with the store
     generated by this evaluation.
     We return two typing contexts, respectively for the names in the domain of the interactive env,
     and the one contains in the values of its image. *)
  val get_typed_ienv :
    in_channel ->
    in_channel ->
    interactive_env * Store.store * name_ctx * name_ctx
end

module type NF = sig
  type ('value, 'ectx, 'fname, 'cname) nf_term

  val is_error : ('value, 'ectx, 'fname, 'cname) nf_term -> bool

  val map_cons :
    f_call:('fname * 'value * 'ectx -> ('fnameb * 'valueb * 'ectxb) * 'a) ->
    f_ret:('cname * 'value -> ('cnameb * 'valueb) * 'a) ->
    f_exn:('cname * 'value -> ('cnameb * 'valueb) * 'a) ->
    f_error:('cname -> 'cnameb * 'a) ->
    ('value, 'ectx, 'fname, 'cname) nf_term ->
    ('valueb, 'ectxb, 'fnameb, 'cnameb) nf_term * 'a

  val map :
    f_cn:('cname -> 'cnameb) ->
    f_fn:('fname -> 'fnameb) ->
    f_val:('value -> 'valueb) ->
    f_ectx:('ectx -> 'ectxb) ->
    ('value, 'ectx, 'fname, 'cname) nf_term ->
    ('valueb, 'ectxb, 'fnameb, 'cnameb) nf_term

  val merge_val_ectx :
    f_val:('value -> 'valueb) ->
    f_merge:('value * 'ectx -> 'valueb) ->
    ('value, 'ectx, 'fname, 'cname) nf_term ->
    ('valueb, unit, 'fname, 'cname) nf_term

  val apply_val :
    'a -> ('value -> 'a) -> ('value, 'ectx, 'fname, 'cname) nf_term -> 'a

  val apply_cons :
    f_call:('fname * 'value * 'ectx -> 'a) ->
    f_ret:('cname * 'value -> 'a) ->
    f_exn:('cname * 'value -> 'a) ->
    f_error:('cname -> 'a) ->
    ('value, 'ectx, 'fname, 'cname) nf_term ->
    'a

  module M : Util.Monad.BRANCH

  val generate_nf_term :
    fname_ctx:('fname, 'value * 'ectx) Util.Pmap.pmap ->
    cname_ctx:('cname, 'value) Util.Pmap.pmap ->
    exn_ctx:('cname, 'value) Util.Pmap.pmap ->
    ('value, 'ectx, 'fname, 'cname) nf_term M.m

  val abstract_nf_term_m :
    f_fn:('fname * 'value * 'ectx -> ('avalue * 'res) M.m) ->
    f_cn:('cname * 'value -> ('avalue * 'res) M.m) ->
    ('value, 'ectx, 'fname, 'cname) nf_term ->
    (('avalue, unit, 'fname, 'cname) nf_term * 'res) M.m

  val equiv_nf_term :
    ('name Util.Namespan.namespan ->
    'value ->
    'value ->
    'name Util.Namespan.namespan option) ->
    'name Util.Namespan.namespan ->
    ('value, 'ectx, 'name, 'name) nf_term ->
    ('value, 'ectx, 'name, 'name) nf_term ->
    'name Util.Namespan.namespan option
end

(* The following signature WITHAVAL_INOUT is used for operational languages
   with normal forms that do not have necessarily a head name.
   This is for example the case of terms that are not named, so
   that values are normal forms. *)
module type WITHAVAL_INOUT = sig
  include COMP
  module Nf : NF with module M = Store.M

  type eval_context
  type typevar
  type typename

  val string_of_typename : typename -> string

  val generate_typename_subst :
    typevar list -> typename list * (typevar, typ) Util.Pmap.pmap

  val apply_type_subst : typ -> (typevar, typ) Util.Pmap.pmap -> typ
  val get_input_type : negative_type -> typevar list * typ
  val get_output_type : negative_type -> typ
  val string_of_eval_context : eval_context -> string

  type normal_form_term = (value, eval_context, Name.name, unit) Nf.nf_term

  val get_nf_term : term -> normal_form_term

  val refold_nf_term :
    (value, unit, negative_val, eval_context) Nf.nf_term -> term

  module AVal :
    Abstract_val.AVAL
      with type name = Name.name
       and type value = value
       and type negative_val = negative_val
       and type typ = typ
       and type negative_type = negative_type
       and type label = Store.label
       and type store_ctx = Store.store_ctx
       and module M = Store.M
end

(* The following signature WITHAVAL_NEG is used for operational languages
   with normal forms that have necessarily a head name.
   This is for example the case of languages with named terms,
   as in the λμ-calculus. *)

module type WITHAVAL_NEG = sig
  include COMP
  module Nf : NF with module M = Store.M

  val negating_type : negative_type -> typ

  type normal_form_term = (value, unit, Name.name, Name.name) Nf.nf_term

  val get_nf_term : term -> normal_form_term

  val refold_nf_term :
    (value, unit, negative_val, negative_val) Nf.nf_term -> term

  module AVal :
    Abstract_val.AVAL
      with type name = Name.name
       and type value = value
       and type negative_val = negative_val
       and type typ = typ
       and type negative_type = negative_type
       and type label = Store.label
       and type store_ctx = Store.store_ctx
       and module M = Store.M
end
